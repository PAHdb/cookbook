

A complete example
====================

A complete example of fitting an observation is shown below. It is an adaption of the 'fit_a_spectrum.pro'-procedure found in the examples-directory of the AmesPAHdbIDLSuite, and is followed by a detailed, line-by-line, explanation of the code.

An observation is read from 'myFile' and the
'AmesPAHdbIDLSuite_CREATE_OBSERVATION_UNITS_S'-helper function is
called to associate units.

.. tabs::

    .. code-tab:: idl

        observation = OBJ_NEW('AmesPAHdbIDLSuite_Observation', 'myFile', $
                               Units=AmesPAHdbIDLSuite_CREATE_OBSERVATION_UNITS_S())

Observation abscissa units are converted to wavenumber. 

.. tabs::

    .. code-tab:: idl

        observation->AbscissaUnitsTo,1

The observation is rebinned onto a uniform grid spaced 5 wavenumbers. 

.. tabs::

    .. code-tab:: idl

        observation->Rebin,5D,/Uniform

The default NASA Ames PAH IR Spectroscopic Database XML-file is loaded 

.. tabs::

    .. code-tab:: idl

        pahdb = OBJ_NEW('AmesPAHdbIDLSuite')

The fundamental vibrational transitions from a subset of PAHs are retrieved. 

.. tabs::

    .. code-tab:: idl

        transitions = pahdb->getTransitionsByUID( $
                      pahdb->Search("mg=0 o=0 fe=0 si=0 chx=0 ch2=0 c>20"))

A FixedTemperature emission model at 600 Kelvin is applied. 

.. tabs::

    .. code-tab:: idl

        transitions->FixedTemperature,600D

The fundamental vibrational transitions are redshifted 15 wavenumbers. 

.. tabs::

    .. code-tab:: idl

        transitions->Shift,-15D

The fundamental vibrational transitions are convolved with Lorentzian
profiles having a full-width-at-half-maximum of 20 cm-1 onto the
observational grid. 

.. tabs::

    .. code-tab:: idl

        spectrum = transitions->Convolve(/Lorentzian, $
                                         Grid=observation->getGrid(), $
                                         FWHM=20D)

The observation is fitted with the PAH emission spectra.

.. tabs::

    .. code-tab:: idl

        fit = spectrum->Fit(observation)

Cleanup of 'spectrum'. 

.. tabs::

    .. code-tab:: idl

        OBJ_DESTROY,spectrum

Display several aspects of the fit. 

.. tabs::

    .. code-tab:: idl

        fit->Plot,/Wavelength
        fit->Plot,/Wavelength,/Residual
        fit->Plot,/Wavelength,/Charge

The transitions are intersected with the PAH species in the fit. 

.. tabs::

    .. code-tab:: idl

        transitions->Intersect, $
                fit->getUIDs()

The fundamental vibrational transitions are again convolved with
Lorentzian profiles having a full-width-at-half-maximum of 20 cm-1,
but now onto a generated grid from 3-10 micron. 

.. tabs::

    .. code-tab:: idl

        spectrum = transitions->Convolve(/Lorentzian, $
                                         FWHM=20D, $
                                         XRange=1D4/[20D, 3D])

The individual PAH spectra are added using weights retrieved from the
fit. 

.. tabs::

    .. code-tab:: idl

        coadded = spectrum->CoAdd(Weights=fit->getWeights())

The coadded spectrum is displayed, revealing the entire 3-20 micron,
predicted, PAH spectrum. 

.. tabs::

    .. code-tab:: idl

        coadded->Plot

Cleanup. 

.. tabs::

    .. code-tab:: idl

        OBJ_DESTROY,[coadded, spectrum, fit, transitions, pahdb, observation]
